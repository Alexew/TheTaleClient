buildscript {
    repositories {
        maven { url 'http://download.crashlytics.com/maven' }
    }

    dependencies {
        classpath 'com.crashlytics.tools.gradle:crashlytics-gradle:1.+'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'crashlytics'

repositories {
    maven { url 'http://download.crashlytics.com/maven' }
}

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.1"

    defaultConfig {
        applicationId "com.lonebytesoft.thetaleclient"
        minSdkVersion 9
        targetSdkVersion 21
        versionCode 12
        versionName "1.4.2"
    }

    signingConfigs {
        release {
            storeFile file('../key/release.keystore')
            storePassword keystorePassword
            keyAlias 'release'
            keyPassword releaseKeyPassword
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    lintOptions {
        abortOnError false
        checkAllWarnings true
    }
}

dependencies {
    compile 'com.crashlytics.android:crashlytics:1.+'
    compile 'com.android.support:appcompat-v7:21.0.0'
    compile 'com.astuetz:pagerslidingtabstrip:1.0.1' // https://github.com/astuetz/PagerSlidingTabStrip
    compile project(':android-support-v4-preferencefragment') // https://github.com/kolavar/android-support-v4-preferencefragment
    compile 'com.github.chrisbanes.photoview:library:1.2.3' // https://github.com/chrisbanes/PhotoView
}

android.applicationVariants.all { variant ->
    variant.getOutputs().each { output ->
        if (output.outputFile != null && output.outputFile.name.endsWith('.apk')) {
            output.outputFile = new File(output.outputFile.parent,
                    variant.buildType.name + "-" + variant.mergedFlavor.versionName + "-b" + variant.mergedFlavor.versionCode + ".apk")
        }
    }
}
