apply plugin: 'com.android.application'
apply plugin: 'com.google.firebase.firebase-crash'
android {
    compileSdkVersion 25
    buildToolsVersion "26-rc1"
    def versionPropsFile = file('version.properties')

    if (versionPropsFile.canRead()) {
        def Properties versionProps = new Properties()

        versionProps.load(new FileInputStream(versionPropsFile))

        def code = versionProps['VERSION_CODE'].toInteger() + 1
        versionProps['VERSION_CODE'] = code.toString()
        versionProps.store(versionPropsFile.newWriter(), null)

        defaultConfig {
            applicationId "com.wrewolf.thetaleclient"
            minSdkVersion 19
            targetSdkVersion 25
            versionCode code
            versionName "1.6.9." + code + "_beta"
            resValue "string", "tray__authority", "${applicationId}.tray"
        }
    } else {
        throw new GradleException("Could not read version.properties!")
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            debuggable false
            jniDebuggable false
            renderscriptDebuggable false
            zipAlignEnabled true
        }
        debug {
            debuggable true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            jniDebuggable true
            renderscriptDebuggable true
        }
    }

    dexOptions {
        preDexLibraries true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    lintOptions {
        abortOnError false
        checkAllWarnings true
    }
}

dependencies {
    // latest versions of libraries as of 15.05.2017
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.google.firebase:firebase-core:10.2.4'
    compile 'com.google.firebase:firebase-messaging:10.2.4'
    compile 'com.google.firebase:firebase-crash:10.2.4'
    compile 'com.squareup.okhttp3:okhttp:3.6.0'
    compile 'com.squareup.okhttp3:okhttp-urlconnection:3.6.0'
    compile 'com.squareup.okio:okio:1.11.0'
    compile 'de.greenrobot:eventbus:2.4.0'
    compile 'net.grandcentrix.tray:tray:0.11.1'
    compile 'com.astuetz:pagerslidingtabstrip:1.0.1' // https://github.com/astuetz/PagerSlidingTabStrip
    compile project(':android-support-v4-preferencefragment') // https://github.com/kolavar/android-support-v4-preferencefragment
    compile 'com.github.chrisbanes.photoview:library:1.2.4' // https://github.com/chrisbanes/PhotoView
    compile 'org.jsoup:jsoup:1.10.2' // https://github.com/jhy/jsoup
    compile 'com.android.support:design:25.3.1'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
}

android.applicationVariants.all { variant ->
    variant.getOutputs().each { output ->
        if (output.outputFile != null && output.outputFile.name.endsWith('.apk')) {
            output.outputFile = new File(output.outputFile.parent,
                    variant.buildType.name + "-" + variant.mergedFlavor.versionName + "-b" + variant.mergedFlavor.versionCode + ".apk")
        }
    }
}

apply plugin: 'com.google.gms.google-services'