buildscript {
    repositories {
        maven { url 'http://download.crashlytics.com/maven' }
    }

    dependencies {
        classpath 'com.crashlytics.tools.gradle:crashlytics-gradle:1.+'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'crashlytics'

repositories {
    maven { url 'http://download.crashlytics.com/maven' }
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.lonebytesoft.thetaleclient"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
    }

    signingConfigs {
        release {
            storeFile file('../key/release.keystore')
            storePassword keystorePassword
            keyAlias 'release'
            keyPassword releaseKeyPassword
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    lintOptions {
        abortOnError false
        checkAllWarnings true
    }
}

dependencies {
    // latest versions of libraries as of 11.05.2015
    compile 'com.crashlytics.android:crashlytics:1.+'
    compile 'com.android.support:appcompat-v7:22.1.1'
    compile 'com.astuetz:pagerslidingtabstrip:1.0.1' // https://github.com/astuetz/PagerSlidingTabStrip
    compile project(':android-support-v4-preferencefragment') // https://github.com/kolavar/android-support-v4-preferencefragment
    compile 'com.github.chrisbanes.photoview:library:1.2.3' // https://github.com/chrisbanes/PhotoView
    compile 'org.jsoup:jsoup:1.8.2' // https://github.com/jhy/jsoup
    compile project(':sdkandroid')
}

android.applicationVariants.all { variant ->
    variant.getOutputs().each { output ->
        if (output.outputFile != null && output.outputFile.name.endsWith('.apk')) {
            output.outputFile = new File(output.outputFile.parent,
                    variant.buildType.name + "-" + variant.mergedFlavor.versionName + "-b" + variant.mergedFlavor.versionCode + ".apk")
        }
    }
}
